plugins {
    id 'java'
}

group = 'dev.hytalemodding'
version = '0.0.8'


repositories {
    mavenCentral()
    mavenLocal()// Crucial: Looks for the HytaleServer.jar you manually installed
    maven {
        name = "hytale-release"
        url = uri("https://maven.hytale.com/release")
    }
    maven {
        name = "hytale-pre-release"
        url = uri("https://maven.hytale.com/pre-release")
    }
}

dependencies {
    // The Hytale Server API
    compileOnly files('libs/HytaleServer.jar')
    testRuntimeOnly files('libs/HytaleServer.jar')

    // Dependencies needed for JSON storage (Gson is standard)
    implementation 'com.google.code.gson:gson:2.10.1'

    // Annotations (Nullable/Nonnull)
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.4'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('copyToDevEnv', Copy) {
    from jar.archiveFile
    into file('C:/Users/devin/Desktop/HytaleServer/Server/mods')
}

tasks.register('copyGameplayConfigToDevEnv', Copy) {
    from file('hyrune_data/gameplay_config.json')
    into file('C:/Users/devin/Desktop/HytaleServer/Server/hyrune_data')
}

tasks.named('copyToDevEnv') {
    finalizedBy('copyGameplayConfigToDevEnv')
}

tasks.named('build') {
    finalizedBy('copyToDevEnv')
}

